apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.akaita.android.easylauncher'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "xyz.hisname.fireflyiii"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 14
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        def appConfigFile = file('config.properties')
        def hostName = ""
        if(appConfigFile.exists()){
            Properties appConfig = new Properties()
            appConfig.load(new FileInputStream(appConfigFile))
            if(appConfig['config.hostname'] != null){
                hostName = appConfig['config.hostname']
                manifestPlaceholders = [host: hostName.replaceAll('"', '')]
            }
        } else {
            throw new FileNotFoundException("$appConfigFile not found!")
        }
        buildConfigField("String", "HOSTNAME", hostName)
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            minifyEnabled false
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    configurations {all*.exclude group: 'com.android.support'}
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$Deps.kotlinVersion"
    // Android support libs
    implementation Deps.appCompat
    implementation Deps.androidxFragment
    implementation Deps.androidxAnnotation
    implementation Deps.androidxRecyclerView
    implementation Deps.materialDesign
    implementation Deps.androidxCore
    implementation Deps.androidxConstraintLayout
    implementation Deps.androidxPref
    implementation Deps.androidxWork

    // Lifecycle component
    implementation Deps.lifecycleLibs
    kapt Deps.lifecycleCompiler

    // Room
    implementation Deps.roomLibs
    kapt Deps.roomCompiler
    // Retrofit
    implementation Deps.retrofitLibs
    implementation Deps.retrofitGson

    // Coroutine
    implementation Deps.coroutineCore
    implementation Deps.coroutineAndroid

    implementation Deps.materialDrawer
    implementation Deps.iconics
    implementation Deps.googleMaterialIcons
    implementation Deps.fontAwesome

    implementation Deps.toasty
    implementation Deps.threeTenBp
    implementation Deps.chart
    implementation Deps.aboutLib

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'
}