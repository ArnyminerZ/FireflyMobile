apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "xyz.hisname.fireflyiii"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        def appConfigFile = file('config.properties')
        def hostName = ""
        if(appConfigFile.exists()){
            Properties appConfig = new Properties()
            appConfig.load(new FileInputStream(appConfigFile))
            if(appConfig['config.hostname'] != null){
                hostName = appConfig['config.hostname']
                manifestPlaceholders = [host: "empty"]
            }
        } else {
            throw new FileNotFoundException("$appConfigFile not found!")
        }
        buildConfigField("String", "HOSTNAME", hostName)
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    configurations {all*.exclude group: 'com.android.support'}
}

dependencies {
    ext.androidSupportLibs = "1.0.0"
    ext.roomVersion = "2.0.0-rc01"
    ext.lifecycleVersion = "2.0.0-rc01"
    ext.retrofitVersion = "2.4.0"
    ext.coroutineVersion = "0.26.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android support libs
    implementation "androidx.appcompat:appcompat:$androidSupportLibs"
    implementation "androidx.fragment:fragment:$androidSupportLibs"
    implementation "androidx.annotation:annotation:$androidSupportLibs"
    implementation "androidx.recyclerview:recyclerview:$androidSupportLibs"
    implementation "com.google.android.material:material:$androidSupportLibs"
    implementation "androidx.core:core-ktx:$androidSupportLibs"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // Lifecycle component
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"
    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    implementation("com.mikepenz:materialdrawer:6.1.0-rc01")
    implementation 'com.github.GrenderG:Toasty:1.3.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

}